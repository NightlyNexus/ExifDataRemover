buildscript {
  ext.versions = [
      'androidGradlePlugin': '4.1.2',
      'compileSdk': 30,
      'coroutines': '1.4.3',
      'kotlin': '1.4.31',
      'leakcanary': '2.6',
      'okio': '2.10.0',
      'supportLibrary': '1.1.0',
  ]

  ext.deps = [
      'androidGradlePlugin': "com.android.tools.build:gradle:$versions.androidGradlePlugin",
      'kotlin': [
          'coroutines': "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines",
          'coroutinesAndroid': "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines",
          'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin",
          'stdlibJdk7': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
      ],
      'leakcanary': "com.squareup.leakcanary:leakcanary-android:$versions.leakcanary",
      'okio': "com.squareup.okio:okio:$versions.okio",
      'supportLibrary': [
          'annotations': "androidx.annotation:annotation:$versions.supportLibrary",
          'appCompat': "androidx.appcompat:appcompat:$versions.supportLibrary",
      ]
  ]

  dependencies {
    classpath deps.androidGradlePlugin
    classpath deps.kotlin.gradlePlugin
  }

  repositories {
    google()
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
}

allprojects {
  repositories {
    google()
    mavenCentral()
    jcenter() // Needed for lint.
  }

  afterEvaluate { project ->
    project.configurations.all {
      resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.version.endsWith('+')) {
          throw new GradleException(
              "Wildcard dependency forbidden: ${requested.group}:${requested.name}:${requested.version}")
        }
      }
    }
  }
}
